// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© ZenAndTheArtOfTrading / PineScriptMastery.com
// @version=6

// CONDITIONAL EXIT

strategy("PineScriptMastery.com | Conditional Exit", overlay=true)

//! User inputs 
float INPUT_SL_DISTANCE     = input.float(1, "SL Distance", display=display.none)
int INPUT_MA_LENGTH         = input.int(50, "MA Length", display=display.none)

//! Indicator values
float atrValue              = ta.atr(14)
float movingAverage         = ta.sma(close, INPUT_MA_LENGTH)

//! Variables
var float tradeStopLoss     = na

//! Entry conditions
bool canTakeTrade           = strategy.position_size == 0 and not na(atrValue) and not na(movingAverage)
bool longEntryCondition     = ta.crossover(close, movingAverage) and barstate.isconfirmed
bool shortEntryCondition    = ta.crossunder(close, movingAverage) and barstate.isconfirmed

//! Exit conditions
bool longExitCondition      = close < low[1] and barstate.isconfirmed
bool shortExitCondition     = close > high[1] and barstate.isconfirmed

//! Check entry conditions
if (canTakeTrade)
    //! Reset SL/TP before placing a new trade
    tradeStopLoss := na

    //! Calculate SL distance in points
    float slDistance = atrValue * INPUT_SL_DISTANCE

    //! LONG ENTRY
    if (longEntryCondition)
        //! Enter
        strategy.entry("Long", strategy.long)
        //! Calculate stop
        tradeStopLoss := close - slDistance

    //! SHORT ENTRY
    if (shortEntryCondition)
        //! Enter
        strategy.entry("Short", strategy.short)
        //! Calculate stop
        tradeStopLoss := close + slDistance

//! Long exit
if (strategy.position_size > 0)
    //! LONG SL
    strategy.exit("Exit Long", from_entry="Long", stop=tradeStopLoss)
    //! LONG EXIT CONDITION 
    if (longExitCondition)
        strategy.close("Long")

//! Short exit
if (strategy.position_size < 0)
    //! SHORT SL
    strategy.exit("Exit Short", from_entry="Short", stop=tradeStopLoss)
    //! SHORT EXIT CONDITION 
    if (shortExitCondition)
        strategy.close("Short")

//! Drawing/debug 
plot(tradeStopLoss, "SL", color=color.red, style=plot.style_linebr)
plot(movingAverage, "MA", color=color.blue)